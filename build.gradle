plugins {
    id 'application'
    id 'io.freefair.lombok' version '8.6'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply from: "gradle/tasks/plugin-creation.gradle"

version = '1.0'

tasks.named('shadowJar') {
    enabled = false
}

// Create a resolvable configuration just for running microbot
configurations {
    microbotRuntime {
        canBeConsumed = false
        canBeResolved = true
    }

    pluginShadowClasspath {
        extendsFrom runtimeClasspath
        exclude group: 'net.runelite', module: 'microbot'
    }
}

repositories {
    maven {
        url = 'http://138.201.81.246:8081/repository/microbot-release/'
        allowInsecureProtocol = true
    }
    mavenCentral()
}

dependencies {
    implementation 'net.runelite:microbot:1.9.6:shaded'
    microbotRuntime 'net.runelite:microbot:1.9.6:shaded'
    implementation 'com.google.guava:guava:33.2.0-jre'
}

application {
    // Replace this with the actual main class inside your shaded jar
    mainClass = 'net.runelite.client.RuneLite'
}

// Run Microbot
tasks.register('runMicrobot', Exec) {
    dependsOn tasks.named('copyToMicrobot')
    commandLine 'java', '-jar', configurations.microbotRuntime.singleFile, ' -plugin=abc'
}


// ---- auto-detect plugins (src/main/java/plugins/<name>) ----
def pluginsDir = file('src/main/java/plugins')
def pluginDirs = pluginsDir.listFiles()?.findAll { it.isDirectory() } ?: []

println "Detected plugins: " + pluginDirs*.name

def selectedPlugin = project.findProperty("plugin") // null if not passed

// Create a sourceSet + ShadowJar per plugin
pluginDirs.each { dir ->
    def pluginName = dir.name

    if (selectedPlugin && selectedPlugin != pluginName) {
        println "Skipping plugin: ${pluginName} (selected: ${selectedPlugin})"
        return // Skip if not matching parameter
    }

    // Create a sourceset for each plugin
    def ss = sourceSets.create(pluginName) {
        java.srcDir dir
        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }

    // Create a ShadowJar task for each plugin
    def jarTask = tasks.register("${pluginName}Jar", ShadowJar) {
        archiveBaseName.set(pluginName)
        archiveVersion.set('')          // => pluginName.jar
        archiveClassifier.set('')       // no -all
        from ss.output
        configurations = [project.configurations.pluginShadowClasspath]
        destinationDirectory.set(layout.buildDirectory.dir("libs"))

        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    def copyTask = tasks.register("copy${pluginName}ToMicrobot", Copy) {
        dependsOn(jarTask)
        from(jarTask.flatMap { it.archiveFile })   // never hardcode build/libs/...
        into("${System.properties['user.home']}/.runelite/microbot-plugins")
        rename { "${pluginName}.jar" }             // optional
    }

    // Only make build depend on it if you want it to always run
    tasks.named("build") {
        dependsOn(jarTask)                         // always build the jar
        dependsOn(copyTask)
    }
}